#!/usr/bin/env zsh

# Desc: interactively search files with rg (with reload) - fullscreen

emulate -LR zsh
setopt extendedglob warncreateglobal

local MATCH; integer MEND MBEGIN
local RG_PREFIX INITIAL_QUERY
local -a rg_filtered f_filtered
local -a opts

zmodload -Fa zsh/zutil +b:zparseopts
zparseopts -E -F -D -a opts - \
    {g,-glob}+:- \
    {i,-iglob}+:- \
    {t,-type}+:- \
    {T,-type-not}+:- \
    n -noignore \
    C -noconfig \
    F -fixed \
    p -pcre -pcre2 \
    {h,-height}:- \
    {b,-bind}:-

rg_filtered=( ${${${(M@)opts:#-(g|-glob(=|))*}##-(g|-glob(=|))}//(#m)*/--glob=${(qq)MATCH}} )
rg_filtered+=( ${${${(M@)opts:#-(t|-type(=|))*}##-(t|-type(=|))}//(#m)*/--type=${(q)MATCH}} )
rg_filtered+=( ${${${(M@)opts:#-(T|-type-not(=|))*}##-(T|-type-not(=|))}//(#m)*/--type-not=${(q)MATCH}} )
rg_filtered+=( ${${${(M)${+opts[(r)-(p|-pcre(2|))]}:#1}:+--pcre2}:-} )
rg_filtered+=( ${${${(M)${+opts[(r)-(F|-fixed)]}:#1}:+--fixed-strings}:-} )
rg_filtered+=( ${${${(M)${+opts[(r)-(n|-noignore)]}:#1}:+--no-ignore}:-} )
rg_filtered+=( ${${${(M)${+opts[(r)-(C|-noconfig)]}:#1}:+--no-config}:-} )

f_filtered=( ${${${(M@)opts:#-(h|-height(=|))*}##-(h|-height(=|))}//(#m)*/--height=${(q)MATCH}} )
f_filtered+=( ${${${(M@)opts:#-(b|-bind(=|))*}##-(b|-bind(=|))}//(#m)*/--bind=${(q-)MATCH}} )

RG_PREFIX="rg --follow --column --hidden --pcre2 --line-number --no-heading --color=always --smart-case ${(j: :)rg_filtered}"
INITIAL_QUERY="${*:-}"
FZF_DEFAULT_COMMAND="$RG_PREFIX ${(qq)INITIAL_QUERY} || true" \
fzf ${(j: :)f_filtered} --ansi \
    --disabled \
    --query="$INITIAL_QUERY" \
    --delimiter : \
    --bind="focus:transform-border-label:echo [ {1} ]" \
    --bind='ctrl-e:become($EDITOR {1} +{2})' \
    --bind='enter:become($EDITOR {1} +{2})' \
    --bind='ctrl-y:execute-silent(xsel -b --trim <<< {1})' \
    --bind="change:reload:sleep 0.1; $RG_PREFIX {q} || true" \
    --preview='bat --style=numbers,changes,snip --color=always --highlight-line {2} -- {1}' \
    --preview-window='default:right:60%:+{2}+3/2:border-left'

    # --bind="change:reload:sleep 0.1; $RG_PREFIX {q} || true" \
    # --preview='bat --style=numbers,header,changes,snip --color=always --highlight-line {2} -- {1}' \
    # --preview-window='default:right:60%:~1:+{2}+3/2:border-left'
