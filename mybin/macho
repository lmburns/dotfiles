#!/usr/bin/env zsh

function macho() {
  local -A opts
  local INITIAL_QUERY
  zmodload -Fa zsh/zutil b:zparseopts
  zparseopts -E -F -D -A opts - s:

  INITIAL_QUERY="${*:-}"

  FZF_DEFAULT_OPTS="
  $FZF_DEFAULT_OPTS
  --ansi
  --query="$INITIAL_QUERY"
  --layout=reverse
  --prompt='Manual: '
  --preview-window=':nohidden'
  --preview='echo {1} | tr -d \"()\" |
      xargs -I{S} batman -Pf --color=always {S} {2} 2>/dev/null'"

  manual=$(\
    apropos -s ${opts[-s]:-''} ${@:-.} \
      | perl -lane \
          'printf "%-8s %s\n",sort($F[1],$F[0]) if !/\(3(p(erl|m)|o|ssl|tiff|caca)\)|__gnu|zmq_|zpool|CURLOPT_/' \
      | fzf  \
      | sed -E 's/^\((.+)\)/\1/'
  )

  # apropos -s '' . | perl -lane 'print join "     ", map {$_->[0]} sort{$a->[1] <=> $b->[1]} map{ [/\((.*)\)/, $_] } @F' B

  [[ -n "$manual" ]] && {
    env \
      LESS_TERMCAP_md=$(tput bold; tput setaf 4) \
      LESS_TERMCAP_me=$(tput sgr0) \
      LESS_TERMCAP_mb=$(tput blink) \
      LESS_TERMCAP_us=$(tput setaf 2) \
      LESS_TERMCAP_ue=$(tput sgr0) \
      LESS_TERMCAP_so=$(tput smso) \
      LESS_TERMCAP_se=$(tput rmso) \
      PAGER="${commands[less]:-$PAGER}" \
      man "${(z)manual}"
  }
}

macho "$@"
