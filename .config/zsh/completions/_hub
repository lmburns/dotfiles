#compdef hub

# Zsh will source this file when attempting to autoload the "_hub" function,
# typically on the first attempt to complete the hub command.  We define two new
# setup helper routines (one for the zsh-distributed version, one for the
# git-distributed, bash-based version).  Then we redefine the "_hub" function to
# call "_git" after some other interception.
#
# This is pretty fragile, if you think about it.  Any number of implementation
# changes in the "_git" scripts could cause problems down the road.  It would be
# better if the stock git completions were just a bit more permissive about how
# it allowed third-party commands to be added.

(( $+functions[__hub_setup_zsh_fns] )) ||
__hub_setup_zsh_fns () {
  (( $+functions[_git-alias] )) ||
  _git-alias () {
    _arguments \
      '-s[output shell script suitable for eval]' \
      '1::shell:(zsh bash csh)'
  }

  (( $+functions[_git-browse] )) ||
  _git-browse () {
    _arguments \
      {-u,--url}'[print the URL]' \
      {-c,--copy}'[copy the URL]' \
      '1::repo:[defaults to user/repo of cwd]' \
      '2::subpage:(wiki commits issues tree)'
  }

  (( $+functions[_git-ci-status] )) ||
  _git-ci-status () {
    _arguments \
      {-v,--verbose}'[print detailed report of all status checks and their URLs]' \
      {-f,--format=}'[pretty print all status checks using FORMAT (implies --verbose)]:format:' \
      '--color=[enabled colored output]:when:(always auto never)' \
      '1:commit:[commit SHA or branch name (default: "HEAD")]'
  }

  (( $+functions[_git-compare] )) ||
  _git-compare () {
    _arguments \
      {-u,--url}'[output the URL]' \
      {-c,--copy}'[copy the URL]' \
      {-b,--base=}'[base branch to compare against in case no explicit arguments were given]:branch:' \
      ':[OWNER] [BASE...]HEAD:'
    }

  (( $+functions[_git-create] )) ||
  _git-create () {
    _arguments \
      '::name (REPOSITORY or ORGANIZATION/REPOSITORY):' \
      {-p,--private}'[make repository private]' \
      {-d,--description=}'[description]:description' \
      {-h,--homepage}'[home page]:repository home page URL:_urls' \
      '--remote-name[set the name for the new git remote (default: "origin")]:remoet:' \
      {-c,--copy}'[copy the URL]'
  }

#       delete:'delete a GitHub repo'
#       fork:'fork origin repo on GitHub'
#       issue:'list or create a GitHub issue'
#       pr:'list or checkout a GitHub pull request'
#       pull-request:'open a pull request on GitHub'
#       release:'list or create a GitHub release'
#       sync:'update local branches from upstream'

  (( $+functions[_git-fork] )) ||
  _git-fork () {
    _arguments \
      '--no-remote[do not add a remote for the new fork]'
  }

  (( $+functions[_git-pull-request] )) ||
  _git-pull-request () {
    _arguments \
      '-f[force (skip check for local commits)]' \
      '-b[base]:base ("branch", "owner\:branch", "owner/repo\:branch"):' \
      '-h[head]:head ("branch", "owner\:branch", "owner/repo\:branch"):' \
      - set1 \
        '-m[message]' \
        '-F[file]' \
        '--no-edit[use first commit message for pull request title/description]' \
        '-a[user]' \
        '-M[milestone]' \
        '-l[labels]' \
      - set2 \
        '-i[issue]:issue number:' \
      - set3 \
        '::issue-url:_urls'
  }

  # stash the "real" command for later
  functions[_hub_orig_git_commands]=$functions[_git_commands]

  # Replace it with our own wrapper.
  declare -f _git_commands >& /dev/null && unfunction _git_commands
  _git_commands () {
    local ret=1
    local curcontext=$curcontext

    # call the original routine
    _call_function ret _hub_orig_git_commands

    # Effectively "append" our hub commands to the behavior of the original
    # _git_commands function.  Using this wrapper function approach ensures
    # that we only offer the user the hub subcommands when the user is
    # actually trying to complete subcommands.
    hub_commands=(
      alias:'show shell instructions for wrapping git'
      browse:'browse the project on GitHub'
      ci-status:'show status of GitHub checks for a commit'
      compare:'open GitHub compare view'
      create:'create new repo on GitHub for the current project'
      delete:'delete a GitHub repo'
      fork:'fork origin repo on GitHub'
      issue:'list or create a GitHub issue'
      pr:'list or checkout a GitHub pull request'
      pull-request:'open a pull request on GitHub'
      release:'list or create a GitHub release'
      sync:'update local branches from upstream'
    )
    _describe -t hub-commands 'hub command' hub_commands && ret=0

    # add user commands
    zstyle -a :completion:$curcontext: user-commands user_commands

    return ret
  }
}

(( $+functions[__hub_setup_bash_fns] )) ||
__hub_setup_bash_fns () {
  # TODO more bash-style fns needed here to complete subcommand args.  They take
  # the form "_git_CMD" where "CMD" is something like "pull-request".

  # Duplicate and rename the 'list_all_commands' function
  eval "$(declare -f __git_list_all_commands | \
        sed 's/__git_list_all_commands/__git_list_all_commands_without_hub/')"

  # Wrap the 'list_all_commands' function with extra hub commands
  __git_list_all_commands() {
    cat <<-EOF
alias
browse
ci-status
compare
create
delete
fork
issue
pr
pull-request
release
sync
EOF
    __git_list_all_commands_without_hub
  }

  # Ensure cached commands are cleared
  __git_all_commands=""
}

# redefine _hub to a much smaller function in the steady state
_hub () {
  # only attempt to intercept the normal "_git" helper functions once
  (( $+__hub_func_replacement_done )) ||
    () {
      # At this stage in the shell's execution the "_git" function has not yet
      # been autoloaded, so the "_git_commands" or "__git_list_all_commands"
      # functions will not be defined.  Call it now (with a bogus no-op service
      # to prevent premature completion) so that we can wrap them.
      if declare -f _git >& /dev/null ; then
        _hub_noop () { __hub_zsh_provided=1 }       # zsh-provided will call this one
        __hub_noop_main () { __hub_git_provided=1 } # git-provided will call this one
        local service=hub_noop
        _git
        unfunction _hub_noop
        unfunction __hub_noop_main
        service=git
      fi

      if (( $__hub_zsh_provided )) ; then
        __hub_setup_zsh_fns
      elif (( $__hub_git_provided )) ; then
        __hub_setup_bash_fns
      fi

      __hub_func_replacement_done=1
    }

  # Now perform the actual completion, allowing the "_git" function to call our
  # replacement "_git_commands" function as needed.  Both versions expect
  # service=git or they will call nonexistent routines or end up in an infinite
  # loop.
  service=git
  declare -f _git >& /dev/null && _git
}

# make sure we actually attempt to complete on the first "tab" from the user
_hub
