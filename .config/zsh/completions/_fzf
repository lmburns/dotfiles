#compdef fzf

_fzf_tiebreak() {
  declare -a tiebreak; tiebreak=(
    length':Prefers line with shorter length'
    begin':Prefers line with matched substring closer to the beginning'
    end':Prefers line with matched substring closer to the end'
    index':Prefers line that appeared earlier in the input stream'
  )
  _describe "tiebreak" tiebreak
}

_fzf_layout() {
  declare -a layout; layout=(
    default':Display from the bottom of the screen'
    reverse':Display from the top of the screen'
    reverse-list':Display from the top of the screen, prompt at the bottom'
  )
  _describe "layout" layout
}

_fzf_info() {
  declare -a info; info=(
    default':Display on the next line to the prompt'
    inline':Display on the same line'
    hidden':Do not display finder info'
  )
  _describe "info" info
}

_fzf() {
  local arguments

  arguments=(
    {-x,--extended}'[extended-search mode]'
    {-e,--exact}'[enable Exact-match]'
    '-i[case-insensitive match (default: smart-case match)]'
    '+i[case-sensitive match]'
    '--literal[do not normalize latin script letters before matching]'
    '--algo=[fuzzy matching algorithm]:algorithm type:(v1 v2):'
    {-n,--nth}'[Comma-separated list of field index expressions]:N[,..]:'
    '--with-nth[Transform the presentation of each line using]:N[,..]:'
    {-d,--delimiter}'[field delimiter regex (default: AWK-style)]:delimiter:'
      '--disabled[do not perform search]'
    '--no-sort[do not sort the result]'
    '+s[do not sort the result]'
    '--tac[reverse the order of the input]'
    '--tiebreak[Comma-separated list of sort criteria to apply]: :_fzf_tiebreak'
    {-m,--multi}'[enable multi-select with tab/shift-tab]'
    '--no-multi[disable multi-select]' \
    '+m[disable multi-select]' \
    '--no-mouse[disable mouse]'
    '--bind[custom key bindings. Refer to the man page]:keybind:'
    '--cycle[enable cyclic scroll]'
    '--keep-right[keep the right end of the line visible on overflow]'
    '--no-hscroll[disable horizontal scroll]'
    '--hscroll-off[number of screen columns to keep to the right of the]:columns:'
    '--filepath-word[make word-wise movements respect path separators]'
    '--jump-labels[label characters for jump and jump-accept]:characters:'

    '--height[display fzf window below the cursor with the given]:height[%]:'
    '--min-height[minimum height when --height is given in percent]:height[%]:'
    '--layout[choose layout]: :_fzf_layout'
    '--reverse[a synonym for --layout=reverse]'
    '--border[draw border around the finder]:border:(rounded sharp horizontal vertical top bottom left right none):'
    '--no-unicode[Use ASCII characters instead of Unicode box drawing characters to draw border.]'
    '--margin[screen margin (TRBL | TB,RL | T,RL,B | T,R,B,L)]:margin:'
    '--padding[padding inside border (TRBL | TB,RL | T,RL,B | T,R,B,L)]:padding:'
    '--info[finder info style]: :_fzf_info'
    '--no-info[A synonym for --info=hidden]'
    '--prompt[input prompt (default: > )]:string:'
    '--pointer[pointer to the current line (default: >)]:string:'
    '--marker[multi-select marker (default: >)]:string:'
    '--header[string to print as header]:string:'
    '--header-lines[the first N lines of the input are treated as header]:N:'
    '--ansi[enable processing of ANSI color codes]'
    '--tabstop[number of spaces for a tab character (default: 8)]:spaces:'
    '--color[base scheme (dark|light|16|bw) and/or custom colors]:[BASE_SCHEME][,COLOR_NAME[\:ANSI_COLOR][\:ANSI_ATTRIBUTES]]...:'
    '--no-bold[do not use bold text]'
    '--black[Use black background . SS History.]'

    '--history[history file]:file:_files'
    '--history-size[maximum number of history entries (default: 1000)]:N:'
    '--preview[command to preview highlighted line ({})]:command:'
    '--preview-window[preview window layout (default: right:50%)]'
    {-q,--query}'[start the finder with the given query]:query:'
    {-1,--select-1}'[automatically select the only match]'
    {-0,--exit-0}'[exit immediately when theres no match]'
    {-f,--filter}'[filter mode. Do not start interactive finder.]'
    '--print-query[print query as the first line]'
    '--expect[comma-separated list of keys to complete fzf]'
    '--read0[read input delimited by ASCII NUL characters]'
    '--print0[print output delimited by ASCII NUL characters]'
    '--sync[synchronous search for multi-staged filtering]'
    '--version[display version information and exit]'
    '*:filename:_files'
  )

  _arguments -s $arguments
}

_fzf "$@"
