# @desc: macho for the use of zle

zmodload -Fa zsh/zutil b:zparseopts

function zmacho() {
  local -A Opts
  local query
  zparseopts -E -D -A Opts - s:

  query=${BUFFER:-}

  export FZF_DEFAULT_OPTS="
  $FZF_DEFAULT_OPTS
  --layout=reverse
  --prompt='Manual: '
  --query="$query"
  --preview-window=':nohidden'
  --preview='echo {1} | sed -E \"s/^\((.+)\)/\1/\" \\
    | xargs -I{S} man -P \"bat --paging=never --color=always -l man\" {S} {2} 2>/dev/null'"

  manual="$(apropos -s ${Opts[-s]:-''} ${@:-.} \
      | grep -vE '^.+ \(0\)' \
      | awk '{print $2 "    " $1}' \
      | sort \
      | fzf  \
      | sed -E 's/^\((.+)\)/\1/'
  )"

  [[ -n "$manual" ]] && {
    set-termcap-env 4 2
    command env $reply man "${(z)manual}"
  }

  # if (($+WIDGET)) {
  #   zle redisplay
  # }
}

zmacho "$@"

# vim: ft=zsh:et:sw=0:ts=2:sts=2
