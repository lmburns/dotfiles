# @desc: lolcate fzf default (HOME)

local -A Opts
zmodload -F zsh/zutil b:zparseopts
zparseopts -E -F -D -A Opts -- u -update d -cd p -print

local mime sel action db
db="default"

(( $+Opts[-u] + $+Opts[--update] )) && lolcate --db $db --update && return 0

if (( $+Opts[-d] + $+Opts[--cd] )) {
  mime="d"
  action='enter:execute(cd {})'
} elif (( $+Opts[-p] + $+Opts[--print] )) {
  mime="d"
  action='enter:accept'
  # action='enter:become({echo {} >/dev/stdout </dev/stdout } &)+abort'
} else {
  mime="f"
  action='enter:execute($EDITOR {})'
}

sel=$(FZF_DEFAULT_COMMAND="lolcate --db '$db' '$@' --mime '$mime' --color=always" \
  fzf --delimiter / \
    --with-nth 4.. \
    --ansi \
    --query "${*:-}" \
    --exit-0 \
    --bind='ctrl-e:become($EDITOR {})' \
    --bind='ctrl-x:execute(cd {})' \
    --bind='ctrl-y:execute-silent(xsel -b --trim <<< {1})' \
    --preview-window=':nohidden,right:65%:wrap' \
    --preview='([[ -f {} ]] && (bat --style=numbers --color=always {})) || ([[ -d {} ]] && (exa -TL 3 --color=always --icons {} | less)) || echo {} 2> /dev/null | head -200'
    # < <( lolcate --db "$db" "$@" --mime "$mime" --color=always )
  )

if [[ -n "$sel" ]] {
  if (( $+Opts[-p] + $+Opts[--print] )) {
    print -r -- "$sel"
  } elif (( $+Opts[-d] + $+Opts[--cd] )) {
    [[ -d "$sel" ]] && builtin cd "$sel"
  } else {
    $EDITOR "$self"
  }
}

# vim: ft=zsh:et:sw=0:ts=2:sts=2:fdm=marker:fmr=[[[,]]]:
