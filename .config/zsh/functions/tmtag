# Desc: use tmsu to either edit a file or cd to dir

local -a sel colored files tags
local cddir

setopt extendedglob

zmodload -Fa zsh/zutil b:zparseopts
zparseopts -D -A opts -- c -cd

if [[ -z "${@}" ]] {
  colored=( ${(@f)"$(tmsu files \
    | xargs tmsu --color=always tags \
  )"} )
} else {
  colored=( ${(@f)"$(tmsu files "${@}" \
    | xargs tmsu --color=always tags \
    )"} )
}

files=( ${(@)${colored[@]//(#m)*/${${(As.:.)MATCH}[1]}}} )
tags=( ${(@)${colored[@]//(#m)*/${${${(As.:.)MATCH}[2]#${${(As.:.)MATCH}[2]%%[! $'\t']*}}%/}}} )

integer i longest=0
local t
local -a packs unpacked
for (( i=1; i<=$#tags; i++ )) {
  packs+=( "%F{14}%B${files[$i]}%f%b: ${tags[$i]}" )
}

sel="$(print -Pln "${(@%)packs}" \
  | hck -d': ' -D $'\t' \
  | column -t \
  | fzf +m --delimiter / \
           --with-nth 4.. \
           --bind='F:execute(lf $(echo {} | hck -f1))'
)"

[[ -n "$sel" ]] && {
  if (( $+opts[-c] + $+opts[--cd] )) {
      cddir="${${(@s: :)sel}[1]}"
      if [[ -d "$cddir" ]] {
          builtin cd "$cddir"
      } else {
          builtin cd "$cddir:h"
      }
  } else {
      $EDITOR "${${(@s: :)sel}[1]}"
  }
}


# vim: ft=zsh:et:sw=0:ts=2:sts=2:fdm=marker:fmr={{{,}}}:
