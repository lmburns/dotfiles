# Desc: use fzf to open bookmark with buku (search by tag first)

local db sql url query
local -a urls

function __bg() {
  nohup "${(z)@}" &>/dev/null &!
}

query=${(j: and tags LIKE :)${(qq)${@//(#m)*/%${MATCH}%}}}
db="$XDG_DATA_HOME/buku/bookmarks.db"
sql="SELECT id,URL,metadata,tags FROM bookmarks WHERE tags LIKE $query;"

urls=($(
  sqlite3 -separator $'\t' "$db" "$sql" \
    | perl -F'\t' -lne 'BEGIN{$,="\t"} $F[3] =~ s/(^,|,$)//g; print @F' \
    | fzf -m \
          --reverse \
          --delimiter '\t' \
          --bind='ctrl-e:execute(buku -w {1} < /dev/tty > /dev/tty)' \
          --preview "buku --nostdin -p {1}" \
          --preview-window=wrap \
    | ${commands[hck]:-cut} -f2
))

for url ($urls[@]) {
  __bg "$BROWSER $url"
}

# vim: ft=zsh:et:sw=0:ts=2:sts=2:fdm=marker:fmr={{{,}}}:
