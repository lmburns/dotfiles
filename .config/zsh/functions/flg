# Desc: lolcate fzf

zmodload -F zsh/zutil b:zparseopts
zparseopts -D -A opts -- u -update c -cd

local mime sel
(( $+opts[-u] + $+opts[--update] )) && lolcate --db git --update && return 0
(( $+opts[-c] + $+opts[--cd] )) && mime="d" || mime="f"

sel=$(\
  lolcate --db git "$@" --mime "$mime" --color=always \
  | fzf \
    --delimiter / \
    --with-nth 6.. \
    --query "${*:-}"
)

[[ -n "$sel" ]] && {
    if (( $+opts[-c] + $+opts[--cd] )) {
        if [[ -d "$sel" ]] {
            builtin cd "$sel"
        } else {
            builtin cd "$sel:h"
        }
    } else {
        ${EDITOR:-vim} "$sel"
    }
}


# vim: ft=zsh:et:sw=0:ts=2:sts=2:fdm=marker:fmr={{{,}}}:
