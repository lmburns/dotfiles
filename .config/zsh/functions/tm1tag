# @desc: use tmsu to either edit a file or cd to dir

local -a sel colored files tags
local file

setopt extendedglob

zmodload -Fa zsh/zutil b:zparseopts b:zformat
zparseopts -D -A opts -- c -cd p -print

if [[ -z "${@}" ]] {
  colored=( ${(@f)"$(tmsu files \
    | xargs tmsu --color=always tags \
  )"} )
} else {
  colored=( ${(@f)"$(tmsu files "${@}" \
    | xargs tmsu --color=always tags \
    )"} )
}

# ft=( ${(@fA)${(@Fs: :)colored}} )

files=( ${(@)${colored[@]//(#m)*/${${(As.:.)MATCH}[1]}}} )
tags=( ${(@)${colored[@]//(#m)*/${${${(As.:.)MATCH}[2]#${${(As.:.)MATCH}[2]%%[! $'\t']*}}%/}}} )

integer i longest=0
local t
local -a packs unpacked
for (( i=1; i<=$#tags; i++ )) {
  packs+=( "%F{14}%B${files[$i]}%f%b: ${tags[$i]}" )
}

sel="$(print -Pln "${(@%)packs}" \
  | hck -d': ' -D $'\t' \
  | column -t \
  | fzf +m --delimiter / \
           --with-nth 4.. \
           --bind='F:execute(lf $(echo {} | hck -f1))'
)"

[[ -n "$sel" ]] && {
  file="${${(@s: :)sel}[1]}"
  if (( $+opts[-c] + $+opts[--cd] )) {
      if [[ -d "$file" ]] {
          builtin cd "$file"
      } else {
          builtin cd "$file:h"
      }
  } elif (( $+opts[-p] + $+opts[--print] )) {
      print -r -- "$file"
  } else {
      if [[ -d "$file" ]] {
          print -Pr -- "%F{53}Not going to edit directory%f"
          return
      }
      $EDITOR -- "$file"
  }
}

# vim: ft=zsh:et:sw=0:ts=2:sts=2:fdm=marker:fmr=[[[,]]]:
