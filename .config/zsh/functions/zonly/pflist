# @desc: lists ZDOTDIR/functions/* with their embedded descriptions

setopt localoptions extendedglob

local -a pfxs lines desc files dirs
dirs=(hooks widgets zeditor zonly)
pfxs=(f1 fl g1 tm1 c1 ls ps t1 n1 x1)    # grouped commands
pfxs+=("*~$ZDOTDIR/functions/(${(j:|:)pfxs}|lps|${(j:|:)dirs})")
pfxs+=(lps hooks widgets zeditor zonly)  # local, uncommited commands
# hooks|widgets|zeditor|zonly

integer pass longest=0 longest2=0
local p file header="# @desc: "

for (( pass = 1; pass <= 2; ++ pass )); do
  for p in "${pfxs[@]}"; do
    files=( $ZDOTDIR/functions/${~p}*(N) )
    if [[ "$p" = ${(j:|:)~dirs} ]]; then
      files=( $ZDOTDIR/functions/${~p}*/*(N) )
    fi

    (( ! ${#files[@]} )) && continue

    if (( pass == 2 )); then
      if (( ${#files[@]} )); then
        if [[ "$p" = "lps" ]]; then
          print -Pr -- "%F{43}${(l:(longest+longest2)/2-4::=:):-} %F{52}%BLOCAL%b %F{43}${(l:longest+longest2-(longest+longest2)/2-3::=:):-}%f"
        elif [[ "$p" = "hooks" ]]; then
          print -Pr -- "%F{43}${(l:(longest+longest2)/2-4::=:):-} %F{52}%BHOOKS%b %F{43}${(l:longest+longest2-(longest+longest2)/2-3::=:):-}%f"
        elif [[ "$p" = "widgets" ]]; then
          print -Pr -- "%F{43}${(l:(longest+longest2)/2-4::=:):-} %F{52}%BWIDGETS%b %F{43}${(l:longest+longest2-(longest+longest2)/2-5::=:):-}%f"
        elif [[ "$p" = "zeditor" ]]; then
          print -Pr -- "%F{43}${(l:(longest+longest2)/2-4::=:):-} %F{52}%BZEDITOR%b %F{43}${(l:longest+longest2-(longest+longest2)/2-5::=:):-}%f"
        elif [[ "$p" = "zonly" ]]; then
          print -Pr -- "%F{43}${(l:(longest+longest2)/2-4::=:):-} %F{52}%BZONLY%b %F{43}${(l:longest+longest2-(longest+longest2)/2-3::=:):-}%f"
        else
          # print -Pr -- "%F{43}${(l:longest+longest2::=:):-}%f"
        fi
      fi
    fi

    for file in "${files[@]}"; do
      [[ -d "$file" ]] && { continue }
      lines=( ${(f)"$(<$file)"} )
      desc=( "${(M)lines[@]:#${header}*}" )
      desc[1]="${desc[1]#$header}"
      file="${file:t}"
      [[ "$file" = (LICENSE|README.md) ]] && continue
      if (( pass == 1 )); then
        (( longest < ${#file} + 3 )) && longest=$(( ${#file} + 3 ))
        (( longest2 < ${#desc[1]} )) && longest2=$(( ${#desc[1]} ))
      else
        print -r -- ${(%):-"%51F%B${file}%b%f${(l:longest-${#file}:: :):- }%54F"}${desc[1]}
      fi
    done
    if (( pass == 2 )); then
      print -Pr -- "%F{1}${(l:longest+longest2::=:):-}%f"
    fi
  done
done

# vim:ft=zsh:et
