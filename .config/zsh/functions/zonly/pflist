# Desc: lists ZDOTDIR/functions/* with their embedded descriptions

setopt localoptions extendedglob

local -a lines desc files

integer pass longest=0 longest2=0
local p file header="# Desc: "

for (( pass = 1; pass <= 2; ++ pass )); do
  files=( $ZDOTDIR/functions/*(N) )
  if (( pass == 2 )); then
    if (( ${#files[@]} )); then
      print -Pr -- "%F{1}${(l:longest+longest2::=:):-}%f"
    fi
  fi
  for file in "${files[@]}"; do
    [[ -d "$file" ]] && continue
    lines=( ${(f)"$(<$file)"} )
    desc=( "${(M)lines[@]:#${header}*}" )
    desc[1]="${desc[1]#$header}"
    file="${file:t}"
    [[ "$file" = (LICENSE|README.md) ]] && continue
    if (( pass == 1 )); then
      (( longest < ${#file} + 3 )) && longest=$(( ${#file} + 3 ))
      (( longest2 < ${#desc[1]} )) && longest2=$(( ${#desc[1]} ))
    else
      print -Pr -- "%F{14}${file}%f${(l:longest-${#file}:: :):- }%F{13}${desc[1]}%f"
    fi
  done
  if (( pass == 2 )); then
    print -Pr -- "%F{1}${(l:longest+longest2::=:):-}%f"
  fi
done

# vim:ft=zsh:et
