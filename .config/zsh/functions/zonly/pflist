# @desc: lists ZDOTDIR/functions/* with their embedded descriptions

setopt localoptions extendedglob warncreateglobal

local -a pfxs lines desc files dirs
# dirs=(hooks lib utils wrap widgets zonly lf)
dirs=( ${(@P)Zdirs[FUNC_D]} )
pfxs=(f1 fl g1 tm1 c1 ls ps t1 n1 x1)    # grouped commands
pfxs+=("*~$ZDOTDIR/functions/(${(j:|:)pfxs}|lps|${(j:|:)dirs})")
# pfxs+=(lps hooks lib utils wrap widgets zonly lf)
pfxs+=(lps ${(@P)Zdirs[FUNC_D]})

integer pass MEND MBEGIN lng1=0 lng2=0
local MATCH p file header="# @desc: "

local function print_pretty() {
  print -Pr -- \
    "%F{43}${(l:(lng1+lng2)/2-4::=:):-} %F{52}%B${1}%b %F{43}${(l:lng1+lng2-(lng1+lng2)/2-${2}::=:):-}%f"
}

for (( pass = 1; pass <= 2; ++ pass )); do
  for p in "${pfxs[@]}"; do
    files=( $ZDOTDIR/functions/${~p}*~*.zwc(|.old)(N) )
    if [[ "$p" = ${(j:|:)~dirs} ]]; then
      files=( $ZDOTDIR/functions/${~p}*/*~*.zwc(|.old)(N) )
    fi

    (( ! ${#files[@]} )) && continue

    if (( pass == 2 )); then
      if (( ${#files[@]} )); then
        case "$p" in
          (lps)         print_pretty "LOCAL" 3  ;;
          ((#m)hooks)   print_pretty "${(U)MATCH}" 3  ;;
          ((#m)lib)     print_pretty "${(U)MATCH}" 1  ;;
          ((#m)utils)   print_pretty "${(U)MATCH}" 5  ;;
          ((#m)wrap)    print_pretty "${(U)MATCH}" 4  ;;
          ((#m)widgets) print_pretty "${(U)MATCH}" 5  ;;
          ((#m)zonly)   print_pretty "${(U)MATCH}" 3  ;;
          ((#m)zeditor) print_pretty "${(U)MATCH}" 5  ;;
          ((#m)lf)      print_pretty "${(U)MATCH}" 0  ;;
        esac
      fi
    fi

    for file in "${files[@]}"; do
      [[ -d "$file" ]] && { continue }
      lines=( ${(f)"$(<$file)"} )
      desc=( "${(M)lines[@]:#${header}*}" )
      desc[1]="${desc[1]#$header}"
      file="${file:t}"
      [[ "$file" = (LICENSE|README.md) ]] && continue
      if (( pass == 1 )); then
        (( lng1 < ${#file} + 3 )) && lng1=$(( ${#file} + 3 ))
        (( lng2 < ${#desc[1]} )) && lng2=$(( ${#desc[1]} ))
      else
        print -r -- ${(%):-"%51F%B${file}%b%f${(l:lng1-${#file}:: :):- }%54F"}${desc[1]}
      fi
    done
    if (( pass == 2 )); then
      print -Pr -- "%F{1}${(l:lng1+lng2::=:):-}%f"
    fi
  done
done

# vim:ft=zsh:et
