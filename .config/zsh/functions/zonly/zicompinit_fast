# @desc: faster & more efficient compinit

setopt extendedglob local_options
autoload -Uz compinit
local zcompf="${ZINIT[ZCOMPDUMP_PATH]:-${ZSH_CACHE_DIR:-$XDG_CACHE_HOME}/zcompdump-${HOST/.*/}-${ZSH_VERSION}}"
zmodload -Fa zsh/parameter p:functions

# use a separate file to determine when to regenerate, as compinit doesn't always need to modify the compdump
local zcompf_a="${zcompf}.augur"

if [[ -e "$zcompf_a" && -f "$zcompf_a"(#qN.md-1) ]]; then
    compinit -C -d "$zcompf"
else
    compinit -d "$zcompf"
    touch "$zcompf_a"
fi

# if zcompdump exists (and is non-zero), and is older than the .zwc file, then regenerate
if [[ -s "$zcompf" && (! -s "${zcompf}.zwc" || "$zcompf" -nt "${zcompf}.zwc") ]]; then
    # since file is mapped, it might be mapped right now (current shells), so rename it then make a new one
    [[ -e "$zcompf.zwc" ]] && command mv -f "$zcompf.zwc" "$zcompf.zwc.old"
    # compile it mapped, so multiple shells can share it (total mem reduction)
    # run in background

    if (( $+functions[zsh-defer] )) {
        zsh-defer -a -c "{ zcompile -M $zcompf && command rm -f $zcompf.zwc.old }"
    } else {
        { zcompile -M "$zcompf" && command rm -f "$zcompf.zwc.old" }&!
    }
fi

# vim: ft=zsh:et:sw=0:ts=2:sts=2
