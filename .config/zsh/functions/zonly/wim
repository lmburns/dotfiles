# @desc: open script in editor

setopt extendedglob
zmodload -Fa zsh/parameter p:commands p:functions

local fdir fullp
local -a paths

# ${${(M)${(j: :)$(whence -va pw)}:#*function*}:+${$(whence -va pw)[-1]}}
# elif [[ -n ${${(M)${(j: :)$(whence -va "$argv")}:#*function*}} ]]; then

fdir="${ZDOTDIR}/functions/${argv}"
fullp=$commands[$argv]

fpath+=(
  $ZDOTDIR/functions/${(@P)^Zdirs[fn_t]}
  $ZDOTDIR/functions
)
# Since I now use compiled paths, add the functions only temporarily
trap 'fpath=( ${fpath[@]:#$ZDOTDIR/functions(|/${~${(j:|/:)${(@P)Zdirs[fn_t]}}})} )' EXIT

if [[ -f "$fdir" ]]; then
  $EDITOR "$fdir"
elif [[ -n $functions[$argv] ]]; then
  paths=( ${(@f):-$(print -rl -- $^fpath/$1(N))} )
  [[ -n "$paths" && -f $paths[1] ]] && {
    $EDITOR "$paths[1]"
  } || $EDITOR ${$(whence -va "$argv")[-1]}
elif { file -b -L --mime-type "${fullp}" | grep -q "text/" }; then
  $EDITOR "$fullp"
else
  print -Pr -- "%F{14}$fullp%f ($(which $fullp)) is not a text file"
fi

# vim: ft=zsh:et:sw=2:ts=2:sts=-1:fdm=marker:fmr=[[[,]]]:
