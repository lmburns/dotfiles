#!/usr/bin/env zsh

# Desc: Helper for vbindkey (taken from marlonrichert)

setopt localoptions extendedglob

autoload -Uz add-zle-hook-widget

case $CONTEXT in
  ( start )
    unset PREDISPLAY POSTDISPLAY

    local opts=${(M)WIDGET##.[@+-]#}
    local cmd=${WIDGET#$opts}
    case $opts in
      ( ^*@* )
        zle .push-line
      ;|
      ( * )
        zle -Rc
      ;|
      ( *-* )
        PS1= RPS1= zle .reset-prompt
      ;|
      ( *[@+]* )
        if [[ -o transientrprompt ]]; then
          RPS1= zle .reset-prompt
          zle -R
        fi
      ;|
      ( *+* )
        print
      ;|
      ( * )
        if [[ $opts == *[@+-]* ]]; then
          eval "${(e)cmd}"
        else
          BUFFER=${(e)WIDGET#.}
        fi
      ;|
      ( *[+-]* )
        local hook=precmd
        local func=${(q):-:${(%):-%N}:$hook:}
        eval "$func() {
          add-zsh-hook -d $hook $func
          unfunction $func
          echoti cuu1
        }"
        add-zsh-hook $hook ${(Q)func}
      ;|
      ( *-* )
        zle .send-break
      ;;
      ( *@* )
        _p9k_precmd
        zle .reset-prompt
      ;;
      ( * )
        zle .accept-line
      ;;
    esac
  ;;
  ( cont )
    local hook=line-init
    local func=${(q):-:${(%):-%N}:$hook:$WIDGET}
    eval "$func() {
      add-zle-hook-widget -d $hook $func
      zle -D $func
      unfunction $func
      zle ${(q)WIDGET} -w
    }"
    add-zle-hook-widget $hook ${(Q)func}
    zle .push-line-or-edit
  ;;
  ( * )
    return 75 # EX_TEMPFAIL; see `man 3 sysexits`.
  ;;
esac

# _p9k_precmd
# zle reset-prompt
# zle -R
