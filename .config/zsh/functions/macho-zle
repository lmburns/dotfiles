# Desc: macho for the use of zle

macho-zle() {
  local -A opts
  local query
  zmodload -Fa zsh/zutil b:zparseopts
  zparseopts -E -D -A opts - s:

  query=${BUFFER:-}

  export FZF_DEFAULT_OPTS="
  $FZF_DEFAULT_OPTS
  --layout=reverse
  --prompt='Manual: '
  --query="$query"
  --preview-window=':nohidden'
  --preview='echo {1} | sed -E \"s/^\((.+)\)/\1/\" \\
    | xargs -I{S} man -P \"bat --paging=never --color=always -l man\" {S} {2} 2>/dev/null'"

  manual="$(apropos -s ${opts[-s]:-''} ${@:-.} \
      | grep -vE '^.+ \(0\)' \
      | awk '{print $2 "    " $1}' \
      | sort \
      | fzf  \
      | sed -E 's/^\((.+)\)/\1/'
  )"

  [[ -n "$manual" ]] && {
    env \
      LESS_TERMCAP_md=$(tput bold; tput setaf 4) \
      LESS_TERMCAP_me=$(tput sgr0) \
      LESS_TERMCAP_mb=$(tput blink) \
      LESS_TERMCAP_us=$(tput setaf 2) \
      LESS_TERMCAP_ue=$(tput sgr0) \
      LESS_TERMCAP_so=$(tput smso) \
      LESS_TERMCAP_se=$(tput rmso) \
      PAGER="${commands[less]:-$PAGER}" \
      man "${(z)manual}"
  }
  zle redisplay
}

macho-zle "$@"

# vim: ft=zsh:et:sw=0:ts=2:sts=2:fdm=marker:fmr=[[[,]]]:
