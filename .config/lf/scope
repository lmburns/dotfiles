#!/usr/bin/env zsh

setopt noclobber noglob nounset extendedglob

# Arguments:
local filepath="${1}"         # Full path of the highlighted file
local width="${2}"
local height="${3}"
local x_coord="${4}"
local Y_COORD="${5}"
integer unhandled=0

local FILE_ABSPATH="${filepath:A}"
local FILE_EXTENSION="${filepath##*.}"
local FILE_EXTENSION_LOWER="${(L)FILE_EXTENSION}"

# Settings:
local HIGHLIGHT_SIZE_MAX=262143  # 256KiB
local HIGHLIGHT_TABwidth=8
local MAXLN=200

# This gives way more specific mimetypes
# i.e., `.lua` = text/x-lua, not text/plain
local MIMETYPE="$(mimetype -b -- "${filepath}")"
# local MIMETYPE="$(file --dereference --brief --mime-type -- "${filepath}")"

# Functions:
try() { output=$(eval '"$@"'); }

# writes the output of the previously used "try" command
dump() { /bin/echo -E "$output"; }

# a common post-processing function used after most commands
trim() { head -n "$MAXLN"; }

prev-image() {
  if [[ -n $DISPLAY && -v FIFO_UEBERZUG ]]; then
    #   "scaling_position_x": 0.5,
    #   "scaling_position_y": 0.5,

    # These load more images if the numbers are strings
    (( $+commands[rjo] )) || { notify-send "Failure" "rjo isn't installed"; exit 1; }

    command rjo \
      "action=add" \
      "identifier=PREVIEW" \
      "x=\"$x_coord\"" \
      "y=\"$Y_COORD\"" \
      "width=\"$(( width - 1 ))\"" \
      "height=\"$(( height - 1 ))\"" \
      "scaler=contain" \
      "path=$1" >"$FIFO_UEBERZUG"
  else
    # Handle non-ueberzug
    case "${MIMETYPE}" in
      (image/*)
        chafa --fill=block --symbols=block --size "$(( width - 4 ))"x"${height}" -- "$1"
      ;;
      (video/*)
        mediainfo -- "$1"
      ;;
     (pdf/*)
        pdftotext -l 10 -nopgbrk -q -- "$1" - && \
          { dump | trim | fmt -s -w $width; exit 0; }
      ;;
    esac
  fi
}

handle_mime() {
  local mimetype="${1}"
  case "${mimetype}" in
    (inode/directory)
      exa -T -- "${filepath}"
    ;;
    (text/html)
      w3m -dump -- "${filepath}"
      # bat -Pf --theme kimbro --style auto -l html -- "${filepath}" ;;
    ;;
    (text/troff)
      # color isn't shown
      MANPAGER="sh -c 'sed -e s/.\\\\x08//g | bat --theme kimbro --color=always -l man -p'" man -- "${filepath}"
    ;;
    (text/csv)
      tidy-viewer -n 30 <$filepath
      # bat -Pf -l csv ${filepath} ;;
    ;;
    (text/* | */xml | application/javascript)
      bat -Pf --tabs "${HIGHLIGHT_TABwidth}" --theme kimbro --style auto -- "${filepath}"
    ;;
    (application/pgp-encrypted)
      bat -Pf --theme kimbro --style auto --j "${filepath}"
    ;;
    (application/zip)
      atool --list -- "${filepath}"
    ;;
    (image/*)
      prev-image "$filepath"
      # prev-image "$filepath" "$width" "$height" "$x_coord" "$Y_COORD"
    ;;
    (audio/*|application/octet-stream)
      mediainfo -- "$filepath"
    ;;
    (video/*)
      CACHE="${XDG_CACHE_HOME}/lf/thumb.$(\
        stat --printf '%n\0%i\0%F\0%s\0%W\0%Y' -- "$FILE_ABSPATH" | sha256sum | hck -f1)"
      [[ ! -f "$CACHE" ]] && command ffmpegthumbnailer -i "$filepath" -o "$CACHE" -s 0
      prev-image "$CACHE"
    ;;
    (*/pdf)
      CACHE="${XDG_CACHE_HOME}/lf/thumb.$(\
        stat --printf '%n\0%i\0%F\0%s\0%W\0%Y' -- "$FILE_ABSPATH" | sha256sum | hck -f1)"
      [[ ! -f "$CACHE.jpg" ]] && command pdftoppm -jpeg -f 1 -singlefile "$1" "$CACHE"
      prev-image "$CACHE.jpg"
    ;;
    (application/vnd.openxmlformats-officedocument.wordprocessingml.document)
      docx2txt -- "${filepath}"
    ;;
    (*)
      unhandled=1
    ;;
  esac
}


handle_other() {
  case "${filepath}" in
    (*.tar)   tar tf -- "${filepath}";;
    (*.rar)   unrar l -- "${filepath}";;
    (*.7z) 7z l -- "${filepath}";;
    (*.[1-8]) man -- "${filepath}" | col -b ;;
    (*.o)     nm -- "${filepath}" | less ;;
    (*.iso)   iso-info --no-header -l -- "${filepath}";;
    (*.doc)   catdoc -- "${filepath}" ;;
    (*.docx)  docx2txt "${filepath}" - ;;
    (*.json)  jq --color-output . -- "${filepath}" || bat -Pf --style auto -l json -- "${filepath}" ;;
    (*.asc|*.log)   bat -Pf --theme kimbro --style auto -- "${filepath}";;
    (*.ipynb) jupview -t KimbieDark -- "${filepath}" ;;
    (*.torrent) transmission-show -- "${filepath}";;
    (*.tgz|*.tar.gz)   tar tzf -- "${filepath}";;
    (*.tar.bz2|*.tbz2) tar tjf -- "${filepath}";;
    (*.tar.txz|*.txz)  xz --list -- "${filepath}";;
    (*.jar|*.war|*.ear|*.oxt)        unzip -l -- "${filepath}";;
    (*.odt|*.ods|*.odp|*.sxw)        odt2txt -- "${filepath}";;
    (*.sha1|*.sha256|*.sha512|*.md5) rhash -c -- "${filepath}" ;;
    (*) (( unhandled )) && { bat -Pf --theme kimbro --style auto -- "${filepath}" } ;;
  esac
}

local -A C
C=(
  3  $'\e[38;5;3m'  12 $'\e[38;5;12m' 13  $'\e[38;5;13m'
  14 $'\e[38;5;14m' 16 $'\e[38;5;16m' 17  $'\e[38;5;17m'
  18 $'\e[38;5;18m' 19 $'\e[38;5;19m' 20  $'\e[38;5;20m'
  21 $'\e[38;5;21m' 22 $'\e[38;5;22m' 42  $'\e[38;5;42m'
  43 $'\e[38;5;43m' 45 $'\e[38;5;45m' 47  $'\e[38;5;47m'
  51 $'\e[38;5;51m' 52 $'\e[38;5;52m' 53  $'\e[38;5;53m'
  U  $'\e[4m'       B  $'\e[1m'       bg  $'\e[3m'
  tb $' \tÂ '        nl $'\n'          res $'\e[0m'
  rnl $'\e[0m\n'
)

handle_fallback() {
  local t mime1 mime2 equals class sep
  local -a mime0
  class="File Type Classification"
  # The ratios of Lf's screen are 1:2:3 by default, and preview is in 3rd screen
  # equals=${${${(M)lf_ratios:#*3}:+(($COLUMNS * 3/6) - ($#class + 2)) / 2}:-18}
  equals=$(( ${lf_ratios##*:} == 3 ? (($COLUMNS * 3/6) - ($#class + 2)) / 2 : 18 ))
  print -rl "$C[nl]\
$C[B]$C[52]${(l:equals::=:):-}$C[res] \
$C[B]$C[42]${class}$C[res] \
$C[B]$C[52]${(l:equals::=:):-}$C[res]" && {
    sep=" $C[18]|$C[17] "
    t="$(file --dereference --brief -- "${filepath}")"
    mime0=( ${${${(@s:,:)t}##[[:space:]]##}%%[[:space:]]##} )
    mime1="$(file --dereference --mime-type --brief -- "${filepath}")"
    mime2="$(mimetype -b -- "${filepath}")"
    stat -c "\
$C[B]$C[16]info:     $C[17]${(pj:$sep:)mime0}$C[rnl]\
$C[B]$C[16]mimetype: $C[18]${mime1}$C[res] $C[B]$C[12]|$C[res] $C[B]$C[18]${mime2}$C[rnl]\
$C[B]$C[16]filetype: $C[43]%F$C[rnl]\
$C[B]$C[16]inode:    $C[53]%i$C[rnl]\
$C[B]$C[16]created:  $C[45]%w$C[rnl]\
$C[B]$C[16]access:   $C[19]%x$C[rnl]\
$C[B]$C[16]modified: $C[12]%z$C[rnl]" -- "${filepath}"
  }
    exit 1
}

handle_mime "${MIMETYPE}"
handle_other
[[ ! -v FIFO_UEBERZUG || $MIMETYPE != (image|video|pdf)/* ]] && handle_fallback

exit 1
